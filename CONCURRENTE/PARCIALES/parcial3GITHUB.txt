programa tho
procesos
  proceso juntarFlores(ES f:numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      f:=f+1
  fin
  proceso irEsquina(ES f:numero E av:numero E ca:numero)
  variables
    a,c:numero
  comenzar
    a:=PosAv
    c:=PosCa
    BloquearEsquina(av,ca)
    Pos(av,ca)
    juntarFlores(f)
    Pos(a,c)
    LiberarEsquina(av,ca)
  fin
  proceso depositarFlores(E f:numero)
  variables
    av,ca:numero
  comenzar
    av:=PosAv
    ca:=PosCa
    repetir f
      BloquearEsquina(10,10)
      Pos(10,10)
      depositarFlor
      Pos(av,ca)
      LiberarEsquina(10,10)
  fin
areas
  cuad:AreaPC(40,40,60,60)
  a1:AreaP(1,1,1,1)
  a2:AreaP(2,1,2,1)
  a3:AreaP(3,1,3,1)
  af:AreaP(4,1,4,1)
  aDep:AreaPC(10,10,10,10)
robots
  robot recolector
  variables
    av,ca,f:numero
    voy:boolean
  comenzar
    f:=0
    RecibirMensaje(voy,fisc)
    mientras voy
      RecibirMensaje(av,fisc)
      RecibirMensaje(ca,fisc)
      irEsquina(f,av,ca)
      RecibirMensaje(voy,fisc)
    EnviarMensaje(V,fisc)
    RecibirMensaje(voy,fisc)
    depositarFlores(f)
    EnviarMensaje(f,fisc)
  fin
  robot fiscalizador
  variables
    id,av,ca,f,fTotal:numero
    listo:boolean
  comenzar
    fTotal:=0
    repetir 8
      Random(id,1,3)
      Random(av,40,60)
      Random(ca,40,60)
      si id=1
        EnviarMensaje(V,r1)
        EnviarMensaje(av,r1)
        EnviarMensaje(ca,r1)
      sino
        si id=2
          EnviarMensaje(V,r2)
          EnviarMensaje(av,r2)
          EnviarMensaje(ca,r2)
        sino
          EnviarMensaje(V,r3)
          EnviarMensaje(av,r3)
          EnviarMensaje(ca,r3)
    EnviarMensaje(F,r1)
    EnviarMensaje(F,r2)
    EnviarMensaje(F,r3)
    repetir 3
      RecibirMensaje(listo,*)
    EnviarMensaje(V,r1)
    EnviarMensaje(V,r2)
    EnviarMensaje(V,r3)
    repetir 3
      RecibirMensaje(f,*)
      fTotal:=f+fTotal
    Informar(fTotal)
  fin
variables
  r1,r2,r3:recolector
  fisc:fiscalizador
comenzar
  AsignarArea(r1,cuad)
  AsignarArea(r2,cuad)
  AsignarArea(r3,cuad)
  AsignarArea(r1,a1)
  AsignarArea(r2,a2)
  AsignarArea(r3,a3)
  AsignarArea(fisc,af)
  AsignarArea(r1,aDep)
  AsignarArea(r2,aDep)
  AsignarArea(r3,aDep)
  Iniciar(r1,1,1)
  Iniciar(r2,2,1)
  Iniciar(r3,3,1)
  Iniciar(fisc,4,1)
fin
  
  
  
  
  
  